import "pkg:/source/enums/KeyCode.bs"
import "pkg:/source/utils/misc.bs"

sub init()
    m.bg = m.top.findNode("bg")
    m.menu = m.top.findNode("menu")
    m.expandAnim = m.top.findNode("expandAnim")
    m.collapseAnim = m.top.findNode("collapseAnim")

    m.menu.observeField("itemSelected", "itemActivated")
    staticRoot = createObject("roSGNode", "ContentNode")

    homeNode = createObject("roSGNode", "ContentNode")
    homeNode.title = "Home"
    homeNode.hdposterurl = "pkg:/images/ic_home.png"
    homeNode.tag = "home"
    homeNode.addField("showLabel", "boolean", m.top.isOpen)
    homeNode.showLabel = m.top.isOpen
    staticRoot.appendChild(homeNode)

    settingsNode = createObject("roSGNode", "ContentNode")
    settingsNode.title = "Settings"
    settingsNode.hdposterurl = "pkg:/images/ic_settings.png"
    settingsNode.tag = "settings"
    settingsNode.addField("showLabel", "boolean", m.top.isOpen)
    settingsNode.showLabel = m.top.isOpen
    staticRoot.appendChild(settingsNode)

    m.menu.content = staticRoot
end sub

sub loadLibraries(libArray as object)
    if not isValid(libArray) then return

    for i = m.menu.content.getChildCount() - 1 to 0 step -1
        n = m.menu.content.getChild(i)
        if left(n.tag, 8) = "library:" then m.menu.content.removeChildIndex(i)
    end for

    insertPos = 1
    for each lib in libArray
        n = createObject("roSGNode", "ContentNode")
        n.title = lib.name
        n.hdposterurl = getLibraryIcon(lib)
        n.tag = "library:" + lib.id

        n.addField("showLabel", "boolean", m.top.isOpen)
        n.showLabel = m.top.isOpen

        m.menu.content.insertChild(n, insertPos)
        insertPos = insertPos + 1
    end for
end sub

function getLibraryIcon(lib as object) as string
    typ = ""
    if isValid(lib.collectionType) then typ = lib.collectionType
    if isValid(lib.json) and isValid(lib.json.CollectionType)
        typ = lib.json.CollectionType
    end if
    typ = lcase(typ)

    if typ = "movies"
        return "pkg:/images/media_type_icons/movie.png"
    else if typ = "tvshows" or typ = "series"
        return "pkg:/images/media_type_icons/tv.png"
    else if typ = "music"
        return "pkg:/images/media_type_icons/music_white.png"
    else if typ = "photos" or typ = "photofolder" or typ = "pictures"
        return "pkg:/images/media_type_icons/photofolder.png"
    else if typ = "livetv" or typ = "live" or typ = "channels"
        return "pkg:/images/media_type_icons/live_tv_white.png"
    else
        return "pkg:/images/ic_library.png"
    end if
end function

sub isOpenChanged()
    isOpen = m.top.isOpen
    sceneContent = m.top.getScene().findNode("content")

    for i = 0 to m.menu.content.getChildCount() - 1
        child = m.menu.content.getChild(i)
        child.showLabel = isOpen
    end for

    if isOpen
        m.menu.itemSize = [316, 100]
        m.expandAnim.control = "start"
        sceneContent.translation = [260, 0]
    else
        m.menu.itemSize = [92, 100]
        m.collapseAnim.control = "start"
        sceneContent.translation = [0, 0]
    end if
end sub

function onKeyEvent(key as string, press as boolean) as boolean
    if not press then return false

    if key = KeyCode.RIGHT or key = KeyCode.BACK
        m.top.isOpen = false

        if isValid(m.top.previousFocus)
            m.top.previousFocus.setFocus(true)
        else
            m.top.getScene().findNode("content").setFocus(true)
        end if
        return true
    end if

    return false
end function

sub itemActivated()
    idx = m.menu.itemSelected
    hr = m.top.getScene().findNode("homeRows")

    if idx = 0 and isValid(hr) and hr.visible
        hr = m.top.getScene().findNode("homeRows")
        if isValid(hr) then hr.setFocus(true)
    end if

    if idx >= 0
        node = m.menu.content.getChild(idx)

        m.top.selectedItem = node.tag

        if left(node.tag, 8) = "library:"
            hr = m.top.getScene().findNode("homeRows")
            if isValid(hr) then hr.selectedItem = node
        end if
    end if

    m.top.isOpen = false
end sub
